apply plugin:'scala'
apply plugin:'application'
apply plugin:'eclipse'

mainClassName = 'com.github.svstuff.systemverilog.Driver'

applicationDefaultJvmArgs = ["-Xmx6g"]

repositories{
    mavenCentral()
    mavenLocal()
}

dependencies{
    compile 'org.scala-lang:scala-library:2.10.4'
    compile 'com.typesafe:scalalogging-slf4j_2.10:1.1.0'
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'org.slf4j:slf4j-simple:1.7.5'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile files('lib/antlr-4.4-complete.jar')
}

compileScala {
    scalaCompileOptions.useCompileDaemon = true
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

task antlr(type: Exec) {
    workingDir = "${projectDir}"
    executable = 'python'
    args = ['./regen.py']
    inputs.files files('regen.py', 'SVParser.g4')
    outputs.files files {
        file("src/main/java/com/github/svstuff/systemverilog/generated").listFiles() +
        file("src/main/scala/com/github/svstuff/systemverilog/generated").listFiles()
    }
}

compileJava.dependsOn antlr
